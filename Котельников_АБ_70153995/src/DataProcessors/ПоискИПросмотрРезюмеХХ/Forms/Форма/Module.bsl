
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	ИмяТекущегоПользователя = ИмяПользователя();
	АдресРедиректа = Константы.АдресРедиректа.Получить();
	
	Если ТекущийПользователь.ИспользоватьСвоеПриложение Тогда
		ТипИспользуемогоПриложения = Перечисления.ТипыПриложенияХХ.Личное;
		НазваниеПриложенияХХ = ТекущийПользователь.НазваниеПриложенияХХ;
		ПочтаРазработчикаХХ = ТекущийПользователь.ПочтаРазработчикаХХ;
		ИдентификаторПриложенияХХ = ТекущийПользователь.ИдентификаторПриложенияХХ;
		СекретПриложенияХХ = ТекущийПользователь.СекретПриложенияХХ;		
	Иначе
		ТипИспользуемогоПриложения = Перечисления.ТипыПриложенияХХ.Общее;
		НазваниеПриложенияХХ = Константы.НазваниеПриложенияХХ.Получить();
		ПочтаРазработчикаХХ = Константы.ПочтаРазработчикаХХ.Получить();
		ИдентификаторПриложенияХХ = Константы.ИдентификаторПриложенияХХ.Получить();
		СекретПриложенияХХ = Константы.СекретПриложенияХХ.Получить();		
	КонецЕсли;
	
	ТокенПользователя = ПолучитьАктуальныйТокен(ТипИспользуемогоПриложения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЭтотОбъект.ВладелецФормы <> Неопределено Тогда
		Вакансия = ЭтотОбъект.ВладелецФормы.Объект.Ссылка;
		НаименованиеРезюме = ЭтотОбъект.ВладелецФормы.Объект.НаименованиеВакансии;
	КонецЕсли;
	
	КоличествоЗаписейНаСтранице = 20;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ОчиститьСообщения();
	
	Если ИсточникВыбора.ИмяФормы = "Справочник.Города.ФормаВыбора" Тогда	
		МассивГородов = ПолучитьДанныеГорода(ВыбранноеЗначение);	
		Для Каждого Город Из МассивГородов Цикл
			НайденныеДубли = СписокГородов.НайтиСтроки(Новый Структура("ИдентификаторГородаХХ", Город.ИдентификаторГорода));
			Если НайденныеДубли.Количество() <> 0 Тогда
				ОбщегоНазначенияКлиент.ВывестиСообщение("Город - """ + Строка(Город.Наименование) + """ уже был выбран");
				Продолжить;
			КонецЕсли;
			НоваяСтрока = СписокГородов.Добавить();
			НоваяСтрока.Город = Город.Наименование;
			НоваяСтрока.ИдентификаторГородаХХ = Город.ИдентификаторГорода;
		КонецЦикла;
		ИспользоватьМестоПроживания = Истина;
		ИзменитьНазваниеГруппыПодборГорода();
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Перечисление.СтатусыПоискаРаботы.ФормаВыбора" Тогда
		НайденныеДубли = СтатусПоискаРаботы.НайтиСтроки(Новый Структура("Статус", ВыбранноеЗначение));
		Если НайденныеДубли.Количество() <> 0 Тогда
			ОбщегоНазначенияКлиент.ВывестиСообщение("Статус - """ + Строка(ВыбранноеЗначение) + """ уже был выбран");
			Возврат;
		КонецЕсли;
		НоваяСтрока = СтатусПоискаРаботы.Добавить();
		НоваяСтрока.Статус = ВыбранноеЗначение;
		ИзменитьНазваниеГруппыСтатусПоискаРаботы();
		ИспользоватьСтатусПоискаРаботы = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
		
	Если ИмяСобытия = "ИзмененыДанныеПользователя" Тогда
		ИспользоватьСвоеПриложение = Источник.Объект.ИспользоватьСвоеПриложение;
		ОбщееПриложение = (ТипИспользуемогоПриложения = ПредопределенноеЗначение("Перечисление.ТипыПриложенияХХ.Общее"));
		СвоеПриложение = (ТипИспользуемогоПриложения = ПредопределенноеЗначение("Перечисление.ТипыПриложенияХХ.Личное"));
		
		Если (СвоеПриложение ИЛИ ОбщееПриложение) И ИспользоватьСвоеПриложение Тогда
			ОбновитьДанныеЛичногоПриложения(Источник);
		ИначеЕсли СвоеПриложение И НЕ ИспользоватьСвоеПриложение Тогда
			ОбновитьДанныеОбщегоПриложения();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВакансияПриИзменении(Элемент)
	
	НаименованиеРезюме = ПолучитьНаименованиеВакансии(Вакансия);
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокГородов

&НаКлиенте
Процедура СписокГородовПослеУдаления(Элемент)	
	ИзменитьНазваниеГруппыПодборГорода();		
КонецПроцедуры

&НаКлиенте
Процедура СтатусПоискаРаботыПослеУдаления(Элемент)
	ИзменитьНазваниеГруппыСтатусПоискаРаботы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ЗапроситьКодАвторизацииПользователя(Команда)
	
	АдресАвторизацииНаХХ = "https://hh.ru/oauth/authorize";
		
	Если ПоказыватьФормуАвторизации Тогда
		СтрокаЗапроса = СтрШаблон("%1?force_login=true&response_type=code&client_id=%2&state=podbor_muiv_%3&redirect_uri=%4", АдресАвторизацииНаХХ, ИдентификаторПриложенияХХ, ИмяТекущегоПользователя, АдресРедиректа);
	Иначе
		СтрокаЗапроса = СтрШаблон("%1?skip_choose_account=true&response_type=code&client_id=%2&state=podbor_muiv_%3&redirect_uri=%4", АдресАвторизацииНаХХ, ИдентификаторПриложенияХХ, ИмяТекущегоПользователя, АдресРедиректа);
	КонецЕсли; 
	
	Попытка
		Ждать ЗапуститьПриложениеАсинх(СтрокаЗапроса);	
	Исключение
		ОбщегоНазначенияКлиент.ВывестиСообщение(ОписаниеОшибки());
	КонецПопытки; 
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПолучитьТокен(Команда)
	
	ОчиститьСообщения();

	ПолученКодАвторизации = Ждать ПолучениеКодаАвторизации();
	Если ПолученКодАвторизации <> Неопределено И НЕ ПолученКодАвторизации Тогда
		Возврат;
	КонецЕсли;
	
	HTTPСоединение = Новый HTTPСоединение("api.hh.ru",,,,,, Новый ЗащищенноеСоединениеOpenSSL());
	HTTPЗапрос = Новый HTTPЗапрос("/token");
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	HTTPЗапрос.Заголовки.Вставить("Authorization", "Bearer " + КодАвторизацииПользователя);
	HTTPЗапрос.Заголовки.Вставить("User-Agent", НазваниеПриложенияХХ + " (" + ПочтаРазработчикаХХ + ")");
	
	ТелоЗапроса = СтрШаблон("grant_type=authorization_code&client_id=%1&client_secret=%2&redirect_uri=%3&code=%4", ИдентификаторПриложенияХХ, СекретПриложенияХХ, АдресРедиректа, КодАвторизацииПользователя);
	HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса);
	
	Попытка
		HTTPОтвет = Ждать HTTPСоединение.ОтправитьДляОбработкиАсинх(HTTPЗапрос);
		Если HTTPОтвет.КодСостояния <> 200 Тогда
			ОбщегоНазначенияКлиент.ВывестиСообщение("Не удалось получить токен. Код ответа - " + HTTPОтвет.КодСостояния);
			Возврат;
		КонецЕсли;
		
		Ответ = ОбщегоНазначенияКлиент.ПрочитатьЗначениеИзJSON(HTTPОтвет.ПолучитьТелоКакСтроку());		
		ЗаписатьТокенПользователяВРегистр(Ответ, ТипИспользуемогоПриложения);
		ТокенПользователя = Ответ.access_token;
		ОбщегоНазначенияКлиент.ВывестиСообщение("Токен успешно получен");
	Исключение
		ОбщегоНазначенияКлиент.ВывестиСообщение(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОбновитьТокен(Команда)
	
	ОчиститьСообщения();
	
	ДанныеСтарогоТокена = ПолучитьТокенОбновленияИзРегистра(ТипИспользуемогоПриложения);
	
	Если ДанныеСтарогоТокена.Свойство("ДатаИВремяОкончанияТокена") Тогда
		ДатаОкончанияДействия = Формат(ДанныеСтарогоТокена.ДатаИВремяОкончанияТокена, "ДЛФ=DDT"); 
		ОбщегоНазначенияКлиент.ВывестиСообщение("Срок действия текущего токена еще не истек. Дата окончания действия - " + ДатаОкончанияДействия);
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ДанныеСтарогоТокена.ТокенОбновления) Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Не найден старый токен. Получите новый.");
		Возврат;
	КонецЕсли;
	
	HTTPСоединение = Новый HTTPСоединение("api.hh.ru",,,,,, Новый ЗащищенноеСоединениеOpenSSL());
	HTTPЗапрос = Новый HTTPЗапрос("/token");
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	HTTPЗапрос.Заголовки.Вставить("Authorization", "Bearer " + ДанныеСтарогоТокена.СтарыйТокен);
	HTTPЗапрос.Заголовки.Вставить("User-Agent", НазваниеПриложенияХХ + " (" + ПочтаРазработчикаХХ + ")");
	
	ТелоЗапроса = СтрШаблон("grant_type=refresh_token&refresh_token=%1", ДанныеСтарогоТокена.ТокенОбновления);
	HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса);
	
	Попытка
		HTTPОтвет = Ждать HTTPСоединение.ОтправитьДляОбработкиАсинх(HTTPЗапрос);
		Если HTTPОтвет.КодСостояния <> 200 Тогда
			ОбщегоНазначенияКлиент.ВывестиСообщение("Не удалось получить токен. Код ответа - " + HTTPОтвет.КодСостояния);
			Возврат;
		КонецЕсли;
		
		Ответ = ОбщегоНазначенияКлиент.ПрочитатьЗначениеИзJSON(HTTPОтвет.ПолучитьТелоКакСтроку());		
		ЗаписатьТокенПользователяВРегистр(Ответ, ТипИспользуемогоПриложения);
		ТокенПользователя = Ответ.access_token;
		ОбщегоНазначенияКлиент.ВывестиСообщение("Токен успешно получен");
	Исключение
		ОбщегоНазначенияКлиент.ВывестиСообщение(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура НайтиРезюме(Команда)
		
	ОчиститьСообщения();
	
	Если ПустаяСтрока(ТокенПользователя) Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Необходимо получить токен");
		Возврат;
	КонецЕсли;
	
	СписокНайденныхРезюме.Очистить();
	СтрокаЗапроса = СформироватьПараметрыРезюме();
	
	HTTPСоединение = Новый HTTPСоединение("api.hh.ru", 443,,,,, Новый ЗащищенноеСоединениеOpenSSL());	
	HTTPЗапрос = Новый HTTPЗапрос("/resumes" + СтрокаЗапроса);
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
	HTTPЗапрос.Заголовки.Вставить("Authorization", "Bearer " + ТокенПользователя);
	HTTPЗапрос.Заголовки.Вставить("User-Agent", НазваниеПриложенияХХ + " (" + ПочтаРазработчикаХХ + ")");
	
	Попытка
		HTTPОтвет = Ждать HTTPСоединение.ПолучитьАсинх(HTTPЗапрос);
		
		Если HTTPОтвет.КодСостояния <> 200 Тогда
			ОбщегоНазначенияКлиент.ВывестиСообщение("Не удалось найти резюме. Код ответа - " + HTTPОтвет.КодСостояния);
			Возврат;
		КонецЕсли;
		
		ОтветJSON = ОбщегоНазначенияКлиент.ПрочитатьЗначениеИзJSON(HTTPОтвет.ПолучитьТелоКакСтроку(), Истина);
		
		НайденоРезультатов = Строка(ОтветJSON["found"]);
		ВсегоСтраниц = Строка(ОтветJSON["pages"]);		
		МассивРезюме = ОтветJSON["items"]; 			
		НомерСтроки = 0;
		
		Для каждого Резюме Из МассивРезюме Цикл		
			СтрокаСписка = СписокНайденныхРезюме.Добавить(); 
			НомерСтроки = НомерСтроки + 1;
			СтрокаСписка.НомерСтроки = НомерСтроки;
			
			СтрокаСписка.АдресРезюме 				= Резюме["alternate_url"];
			СтрокаСписка.ИдентификаторРезюме 		= Резюме["id"];
			СтрокаСписка.ЖелаемаяДолжность 			= Резюме["title"];		
			СтрокаСписка.Возраст 					= Резюме["age"];
			СтрокаСписка.ГородПроживания 			= Резюме["area"]["name"];
			СтрокаСписка.КонтактыДоступны 			= Резюме["can_view_full_info"];		
			ДатаСозданияРезюме 						= ОбщегоНазначенияКлиент.ПолучитьФорматДатыХХ(Резюме["created_at"]);			
			СтрокаСписка.ДатаСозданияРезюме 		= ДатаСозданияРезюме;	
			СтрокаСписка.СсылкаНаСкачиваниеРезюме 	= Резюме["download"]["pdf"]["url"];
			СтрокаСписка.УровеньОБразования 		= Резюме["education"]["level"]["name"];
			
			Если Резюме["gender"] <> Неопределено Тогда 
				СтрокаСписка.ПолСоискателя = Резюме["gender"]["name"];
			КонецЕсли;
			
			ОжидаемаяЗарплата = Резюме["salary"];
			Если Резюме.Получить("salary") <> Неопределено Тогда
				СтрокаСписка.ОжидаемаяЗарплата = Строка(ОжидаемаяЗарплата["amount"]) + " (" + ОжидаемаяЗарплата["currency"] + ")"; 
			КонецЕсли;
			
			ОпытРаботыСоискателя = Резюме["total_experience"];
			
			Если ОпытРаботыСоискателя <> Неопределено Тогда	
				КоличествоОтработанныхЛет = Строка(Цел(ОпытРаботыСоискателя["months"] / 12));
				КоличествоОтработанныхМесяцев = Строка(ОпытРаботыСоискателя["months"] % 12);
				
				Если КоличествоОтработанныхМесяцев <> "0" Тогда
					СтрокаСписка.ОпытРаботыКолонка = КоличествоОтработанныхЛет + " лет " + КоличествоОтработанныхМесяцев + " месяцев"; 
				Иначе
					СтрокаСписка.ОпытРаботыКолонка = КоличествоОтработанныхЛет + " лет"
				КонецЕсли;
				
			КонецЕсли;				
			ДатаПоследнегоОбновления = ОбщегоНазначенияКлиент.ПолучитьФорматДатыХХ(Резюме["updated_at"]);			
			СтрокаСписка.ПоследнееОбновление = ДатаПоследнегоОбновления;					
		КонецЦикла;		
		Всего = СписокНайденныхРезюме.Количество();	
	Исключение
		ОбщегоНазначенияКлиент.ВывестиСообщение(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОткрытьРезюмеВБраузере(Команда)
	
	ОчиститьСообщения();
		
	ТекущаяСтрокаТЧ = Элементы.СписокНайденныхРезюме.ТекущиеДанные; 
	
	Если ТекущаяСтрокаТЧ = Неопределено Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Выберите резюме");
		Возврат;
	КонецЕсли;
	
	АдресРезюме = ТекущаяСтрокаТЧ.АдресРезюме;	
	Ждать ЗапуститьПриложениеАсинх(АдресРезюме);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ДобавитьКандидата(Команда)
	
	ОчиститьСообщения(); 
		
	Если ПустаяСтрока(ТокенПользователя) Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Получите токен");
		Возврат;
	КонецЕсли;
		
	ТекущаяСтрокаТЧ = Элементы.СписокНайденныхРезюме.ТекущиеДанные; 
	
	Если ТекущаяСтрокаТЧ = Неопределено Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Выберите резюме");
		Возврат;
	КонецЕсли; 
	
	РезультатПоиска = НайтиДубльКандидатаПоРезюме(ТекущаяСтрокаТЧ.АдресРезюме); 
	Если РезультатПоиска.НайденДубль Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Кандидат с таким резюме уже был добавлен. Код элемента - " + РезультатПоиска.КодКандидата);
		Возврат;
	КонецЕсли;
	
	ИдентификаторРезюме = ТекущаяСтрокаТЧ.ИдентификаторРезюме;
	
	HTTPСоединение = Новый HTTPСоединение("api.hh.ru", 443,,,,, Новый ЗащищенноеСоединениеOpenSSL());
	HTTPЗапрос = Новый HTTPЗапрос("/resumes/" + ИдентификаторРезюме);
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
	HTTPЗапрос.Заголовки.Вставить("Authorization", "Bearer " + ТокенПользователя);
	HTTPЗапрос.Заголовки.Вставить("User-Agent", НазваниеПриложенияХХ + " (" + ПочтаРазработчикаХХ + ")");
	
	Попытка
		HTTPОтвет = Ждать HTTPСоединение.ПолучитьАсинх(HTTPЗапрос);
		
		Если HTTPОтвет.КодСостояния <> 200 Тогда
			ОбщегоНазначенияКлиент.ВывестиСообщение("Не удалось добавить кандидата. Код ответа - " + HTTPОтвет.КодСостояния); 
			Возврат;
		КонецЕсли;
		
		ОтветJSON = ОбщегоНазначенияКлиент.ПрочитатьЗначениеИзJSON(HTTPОтвет.ПолучитьТелоКакСтроку(), Истина);	
		СозданиеЭлементаСправочникаКандидата(ОтветJSON);		
	Исключение
		ОбщегоНазначенияКлиент.ВывестиСообщение(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура СкачатьРезюмеКандидата(Команда)
	
	ОчиститьСообщения();
	
	Если ПустаяСтрока(ТокенПользователя) Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Получите токен");
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрокаТЧ = Элементы.СписокНайденныхРезюме.ТекущиеДанные; 	
	Если ТекущаяСтрокаТЧ = Неопределено Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Выберите резюме");
		Возврат;
	КонецЕсли;
	
	СсылкаНаРезюме = Сред(ТекущаяСтрокаТЧ.СсылкаНаСкачиваниеРезюме, 18);
	
	HTTPСоединение = Новый HTTPСоединение("api.hh.ru", 443,,,,, Новый ЗащищенноеСоединениеOpenSSL());
	HTTPЗапрос = Новый HTTPЗапрос(СсылкаНаРезюме);
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/pdf");
	HTTPЗапрос.Заголовки.Вставить("Authorization", "Bearer " + ТокенПользователя);
	HTTPЗапрос.Заголовки.Вставить("User-Agent", НазваниеПриложенияХХ + " (" + ПочтаРазработчикаХХ + ")");
	
	Попытка
		HTTPОтвет = Ждать HTTPСоединение.ПолучитьАсинх(HTTPЗапрос);		
		Если HTTPОтвет.КодСостояния <> 200 Тогда
			ОбщегоНазначенияКлиент.ВывестиСообщение("Не удалось скачать резюме. Код ответа - " + HTTPОтвет.КодСостояния);
			Возврат;
		КонецЕсли;
		
		ФайлПДФ = HTTPОтвет.ПолучитьТелоКакДвоичныеДанные();
				
		Режим = РежимДиалогаВыбораФайла.Сохранение; 
		ДиалогСохраненияФайла = Новый ДиалогВыбораФайла(Режим);              
		ДиалогСохраненияФайла.ПолноеИмяФайла = Лев(Строка(Вакансия), 19) + ТекущаяСтрокаТЧ.ЖелаемаяДолжность + "(" + Формат(ТекущаяСтрокаТЧ.ДатаСозданияРезюме, "ДЛФ=ДД") + "-" + Формат(ТекущаяСтрокаТЧ.ПоследнееОбновление, "ДЛФ=ДД") + ")" + ".pdf"; 
		ДиалогСохраненияФайла.МножественныйВыбор = Ложь; 
		ДиалогСохраненияФайла.Заголовок = "Выберите место сохранения резюме";
		
		МестоСохранения = Ждать ДиалогСохраненияФайла.ВыбратьАсинх();				
		Если МестоСохранения = Неопределено Тогда;
			 Возврат;
		КонецЕсли;
		 
		ПолныйПутьКФайлу = МестоСохранения[0];	
		Ждать ФайлПДФ.ЗаписатьАсинх(ПолныйПутьКФайлу);
			
		КнопкиВопроса = Новый СписокЗначений;
		КнопкиВопроса.Добавить("ОткрытьЗдесь", "Открыть в 1С");
		КнопкиВопроса.Добавить("ОткрытьВБраузере", "Открыть в браузере");
		КнопкиВопроса.Добавить("Нет");
		
		ОтветПользователя = Ждать ВопросАсинх("Открыть резюме после скачивания?", КнопкиВопроса,, "Нет", "Открыть резюме");
		
		Если ОтветПользователя = "ОткрытьЗдесь" Тогда
			ДокументPDF = Новый ДокументPDF();
			Ждать ДокументPDF.ПрочитатьАсинх(ПолныйПутьКФайлу);					
			ДокументPDF.Показать("Резюме кандидата - " + ТекущаяСтрокаТЧ.ЖелаемаяДолжность);
		ИначеЕсли ОтветПользователя = "ОткрытьВБраузере" Тогда
			Ждать ЗапуститьПриложениеАсинх(ПолныйПутьКФайлу);
		КонецЕсли	
	Исключение 
		ОбщегоНазначенияКлиент.ВывестиСообщение(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьГород(Команда)
	
	ПараметрыФормы = Новый Структура("ЗакрыватьПриВыборе, МножественныйВыбор", Ложь, Истина);	
	ОткрытьФорму("Справочник.Города.ФормаВыбора", ПараметрыФормы, ЭтотОбъект); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСписокГородов(Команда)
	СписокГородов.Очистить();
	ИзменитьНазваниеГруппыПодборГорода();
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьСтатусПоискаРаботы(Команда)
	
 	ПараметрыФормы = Новый Структура("ЗакрыватьПриВыборе", Ложь);	
	ОткрытьФорму("Перечисление.СтатусыПоискаРаботы.ФормаВыбора", ПараметрыФормы, ЭтотОбъект);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьАктуальныйТокен(ТипИспользуемогоПриложения)

	ОтборПользователя = Новый Структура("Пользователь, ТипПриложения", ПараметрыСеанса.ТекущийПользователь, ТипИспользуемогоПриложения);
	ТокенПользователя = РегистрыСведений.ТокеныПользователей.Получить(ОтборПользователя).Токен;

	Возврат ТокенПользователя; 

КонецФункции

&НаКлиенте
Процедура ОбновитьДанныеЛичногоПриложения(Источник)
	
	ТипИспользуемогоПриложения = ПредопределенноеЗначение("Перечисление.ТипыПриложенияХХ.Личное");
	НазваниеПриложенияХХ = Источник.Объект.НазваниеПриложенияХХ;
	ПочтаРазработчикаХХ = Источник.Объект.ПочтаРазработчикаХХ;
	ИдентификаторПриложенияХХ = Источник.Объект.ИдентификаторПриложенияХХ;
	СекретПриложенияХХ = Источник.Объект.СекретПриложенияХХ;
	ТокенПользователя = ПолучитьАктуальныйТокен(ТипИспользуемогоПриложения);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеОбщегоПриложения()

	ДанныеОбщегоПриложения = ПолучитьДанныеОбщегоПриложения();
	
	ТипИспользуемогоПриложения = ПредопределенноеЗначение("Перечисление.ТипыПриложенияХХ.Общее");
	НазваниеПриложенияХХ = ДанныеОбщегоПриложения.НазваниеПриложенияХХ;
	ПочтаРазработчикаХХ = ДанныеОбщегоПриложения.ПочтаРазработчикаХХ;
	ИдентификаторПриложенияХХ = ДанныеОбщегоПриложения.ИдентификаторПриложенияХХ;
	СекретПриложенияХХ = ДанныеОбщегоПриложения.СекретПриложенияХХ;
	АдресРедиректа = ДанныеОбщегоПриложения.АдресРедиректа;
	ТокенПользователя = ПолучитьАктуальныйТокен(ТипИспользуемогоПриложения);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеОбщегоПриложения()

	ДанныеОбщегоПриложения = Новый Структура;
	ДанныеОбщегоПриложения.Вставить("НазваниеПриложенияХХ", Константы.НазваниеПриложенияХХ.Получить());
	ДанныеОбщегоПриложения.Вставить("ПочтаРазработчикаХХ", Константы.ПочтаРазработчикаХХ.Получить());
	ДанныеОбщегоПриложения.Вставить("ИдентификаторПриложенияХХ", Константы.ИдентификаторПриложенияХХ.Получить());
	ДанныеОбщегоПриложения.Вставить("СекретПриложенияХХ", Константы.СекретПриложенияХХ.Получить());
	ДанныеОбщегоПриложения.Вставить("АдресРедиректа", Константы.АдресРедиректа.Получить());
	ДанныеОбщегоПриложения.Вставить("ТокенПользователя", ПолучитьАктуальныйТокен(Перечисления.ТипыПриложенияХХ.Общее));
	
	Возврат ДанныеОбщегоПриложения; 

КонецФункции

&НаКлиенте
Асинх Функция ПолучениеКодаАвторизации()
	
	HTTPСоединение = Новый HTTPСоединение("taukpebkr3.temp.swtest.ru");
	
	СтрокаЗапроса = СтрШаблон("/includes/get_authorization_code_xx.php?username=%1", ИмяТекущегоПользователя);
	HTTPЗапрос = Новый HTTPЗапрос(СтрокаЗапроса);
	Попытка
		HTTPОтвет = Ждать HTTPСоединение.ПолучитьАсинх(HTTPЗапрос);
		Если HTTPОтвет.КодСостояния <> 200 Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Ответ = ОбщегоНазначенияКлиент.ПрочитатьЗначениеИзJSON(HTTPОтвет.ПолучитьТелоКакСтроку());
		
		Если Ответ.Количество() <> 0 Тогда
			КодАвторизацииПользователя = Ответ[0].authorization_code;
		Иначе
			ОбщегоНазначенияКлиент.ВывестиСообщение("Запросите код авторизации для получения токена");
		КонецЕсли;		
	Исключение
		ОбщегоНазначенияКлиент.ВывестиСообщение(ОписаниеОшибки());
	КонецПопытки;
		
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаписатьТокенПользователяВРегистр(ДанныеТокена, ТипИспользуемогоПриложения)
	
	Токен = ДанныеТокена.access_token;
	ВремяЖизниТокена = ДанныеТокена.expires_in;
	ТокенДляПродления = ДанныеТокена.refresh_token;
		
	НоваяЗапись = РегистрыСведений.ТокеныПользователей.СоздатьМенеджерЗаписи();
	НоваяЗапись.Пользователь = ПараметрыСеанса.ТекущийПользователь;
	НоваяЗапись.ТипПриложения = ТипИспользуемогоПриложения; 
	НоваяЗапись.Токен = Токен;
	НоваяЗапись.ВремяЖизниТокена = ВремяЖизниТокена;
	НоваяЗапись.ТокенДляПродления = ТокенДляПродления;
	НоваяЗапись.ДатаИВремяОкончанияТокена = ТекущаяДата() + ВремяЖизниТокена;
	НоваяЗапись.Записать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТокенОбновленияИзРегистра(ТипИспользуемогоПриложения)

	ИнформацияОТокене = РегистрыСведений.ТокеныПользователей.СоздатьМенеджерЗаписи();
	ИнформацияОТокене.Пользователь = ПараметрыСеанса.ТекущийПользователь;
	ИнформацияОТокене.ТипПриложения = ТипИспользуемогоПриложения; 
	ИнформацияОТокене.Прочитать();  
	
	ДанныеСтарогоТокена = Новый Структура;
	
	Если ИнформацияОТокене.ДатаИВремяОкончанияТокена >= ТекущаяДата() Тогда
		ДанныеСтарогоТокена.Вставить("ДатаИВремяОкончанияТокена", ИнформацияОТокене.ДатаИВремяОкончанияТокена); 
		Возврат ДанныеСтарогоТокена;
	КонецЕсли;
			
	Если НЕ ИнформацияОТокене.Выбран() Тогда
		ДанныеСтарогоТокена.Вставить("ТокенОбновления", "");
		Возврат ДанныеСтарогоТокена;
	КонецЕсли; 
	
	ДанныеСтарогоТокена.Вставить("СтарыйТокен", ИнформацияОТокене.Токен);
	ДанныеСтарогоТокена.Вставить("ТокенОбновления", ИнформацияОТокене.ТокенДляПродления);
	
	Возврат ДанныеСтарогоТокена; 
	
КонецФункции

&НаКлиенте
Функция СформироватьПараметрыРезюме()
			
	// Поисковая фраза
	СтрокаЗапроса = "?text=" + НаименованиеРезюме;
	
	// При использовании дополнительных text.* полей, необходимо указывать весь набор (триаду) параметров
	Если НЕ КакПроизводитсяПоиск.Пустая() И НЕ ГдеВстречаютсяСлова.Пустая() И НЕ ПериодОпытаРаботы.Пустая() Тогда
		Если КакПроизводитсяПоиск = ПредопределенноеЗначение("Перечисление.ВариантыПоискаКлючевыхСлов.ВсеСловаВстречаются") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.logic=all";
		ИначеЕсли КакПроизводитсяПоиск = ПредопределенноеЗначение("Перечисление.ВариантыПоискаКлючевыхСлов.ЛюбоеИзСловВстречается") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.logic=any";
		ИначеЕсли КакПроизводитсяПоиск = ПредопределенноеЗначение("Перечисление.ВариантыПоискаКлючевыхСлов.ТочнаяФразаВстречается") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.logic=phrase";
		ИначеЕсли КакПроизводитсяПоиск = ПредопределенноеЗначение("Перечисление.ВариантыПоискаКлючевыхСлов.НеВстречаются") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.logic=except";
		КонецЕсли; 
		
		Если ГдеВстречаютсяСлова = ПредопределенноеЗначение("Перечисление.МестаВстречиСлов.Везде") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.field=everywhere";
		ИначеЕсли ГдеВстречаютсяСлова = ПредопределенноеЗначение("Перечисление.МестаВстречиСлов.ВНазванииРезюме") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.field=title";
		ИначеЕсли ГдеВстречаютсяСлова = ПредопределенноеЗначение("Перечисление.МестаВстречиСлов.ВОбразовании") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.field=education";
		ИначеЕсли ГдеВстречаютсяСлова = ПредопределенноеЗначение("Перечисление.МестаВстречиСлов.ВКлючевыхНавыках") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.field=skills";
		ИначеЕсли ГдеВстречаютсяСлова = ПредопределенноеЗначение("Перечисление.МестаВстречиСлов.ВОпытеРаботы") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.field=experience"; 
		ИначеЕсли ГдеВстречаютсяСлова = ПредопределенноеЗначение("Перечисление.МестаВстречиСлов.ВКомпанияхИОтраслях") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.field=experience_company"; 
        ИначеЕсли ГдеВстречаютсяСлова = ПредопределенноеЗначение("Перечисление.МестаВстречиСлов.ВДолжностях") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.field=experience_position";
		ИначеЕсли ГдеВстречаютсяСлова = ПредопределенноеЗначение("Перечисление.МестаВстречиСлов.ВОбязанностях") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.field=experience_description"; 
		КонецЕсли;
		
		Если ПериодОпытаРаботы = ПредопределенноеЗначение("Перечисление.ПериодыОпытаРаботы.ЗаВсеВремяОпыта") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.period=all_time";
		ИначеЕсли ПериодОпытаРаботы = ПредопределенноеЗначение("Перечисление.ПериодыОпытаРаботы.ЗаПоследнийГод") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.period=last_year"; 
		ИначеЕсли ПериодОпытаРаботы = ПредопределенноеЗначение("Перечисление.ПериодыОпытаРаботы.ЗаПоследниеТриГода") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.period=last_three_years";
		ИначеЕсли ПериодОпытаРаботы = ПредопределенноеЗначение("Перечисление.ПериодыОпытаРаботы.ЗаПоследниеШестьЛет") Тогда
			СтрокаЗапроса = СтрокаЗапроса + "&text.period=last_six_years";
		КонецЕсли;
	КонецЕсли;
	
	// номер страницы
	СтрокаЗапроса = СтрокаЗапроса + "&page=" + Строка(НомерСтраницы);
	
	// количество записей на странице
	СтрокаЗапроса = СтрокаЗапроса  + "&per_page=" + Строка(КоличествоЗаписейНаСтранице);
	
	Если ВозрастНижняяГраница <> 0 Тогда
		СтрокаЗапроса = СтрокаЗапроса + "&age_from=" + Строка(ВозрастНижняяГраница);		
	КонецЕсли;
	
	Если ВозрастВерхняяГраница <> 0 Тогда
		СтрокаЗапроса = СтрокаЗапроса + "&age_to=" + Строка(ВозрастВерхняяГраница);		
	КонецЕсли;  
	
	Если ВсеРезюмеСУказаннымВозрастом Тогда
		СтрокаЗапроса = СтрокаЗапроса + "&label=only_with_age";
	КонецЕсли; 
	
	Если ПериодПоискаРезюме.ДатаНачала <> Дата('00010101') Тогда 		
		СтрокаЗапроса = СтрокаЗапроса + "&date_from=" + Формат(ПериодПоискаРезюме.ДатаНачала, "ДФ=yyyy-MM-dd");	
	КонецЕсли; 
	
	Если ПериодПоискаРезюме.ДатаОкончания <> Дата('00010101') Тогда 		
		СтрокаЗапроса = СтрокаЗапроса + "&date_to=" + Формат(ПериодПоискаРезюме.ДатаОкончания, "ДФ=yyyy-MM-dd");	
	КонецЕсли;
	
	Если НЕ ТипЗанятостиСоискателя.Пустая() Тогда		
		СтрокаЗапроса = СтрокаЗапроса + ?(ТипЗанятостиСоискателя = ПредопределенноеЗначение("Перечисление.ТипыЗанятости.ПолнаяЗанятость"), "&employment=full", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(ТипЗанятостиСоискателя = ПредопределенноеЗначение("Перечисление.ТипыЗанятости.ЧастичнаяЗанятость"), "&employment=part", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(ТипЗанятостиСоискателя = ПредопределенноеЗначение("Перечисление.ТипыЗанятости.ПроектнаяРабота"), "&employment=project", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(ТипЗанятостиСоискателя = ПредопределенноеЗначение("Перечисление.ТипыЗанятости.Волонтерство"), "&employment=volunteer", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(ТипЗанятостиСоискателя = ПредопределенноеЗначение("Перечисление.ТипыЗанятости.Стажировка"), "&employment=probation", "");
	КонецЕсли;
	
	Если НЕ ОпытРаботы.Пустая() Тогда		
		СтрокаЗапроса = СтрокаЗапроса + ?(ОпытРаботы = ПредопределенноеЗначение("Перечисление.ОпытРаботы.НетОпыта"), "&experience=noExperience", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(ОпытРаботы = ПредопределенноеЗначение("Перечисление.ОпытРаботы.ОтОдногоГодаДоТрехЛет"), "&experience=between1And3", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(ОпытРаботы = ПредопределенноеЗначение("Перечисление.ОпытРаботы.ОтТрехДоШестиЛет"), "&experience=between3And6", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(ОпытРаботы = ПредопределенноеЗначение("Перечисление.ОпытРаботы.БолееШестиЛет"), "&experience=moreThan6", "");
	КонецЕсли;
	
	Если НЕ ПолСоискателя.Пустая() Тогда
		СтрокаЗапроса = СтрокаЗапроса + ?(ПолСоискателя = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Мужской"), "&gender=male", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(ПолСоискателя = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Женский"), "&gender=female", "");
	КонецЕсли;
	
	Если ПолОбязателен Тогда
		СтрокаЗапроса = СтрокаЗапроса + "&label=only_with_gender";
	КонецЕсли;

	Если НЕ ВариантПроживания.Пустая() Тогда
		СтрокаЗапроса = СтрокаЗапроса + ?(ВариантПроживания = ПредопределенноеЗначение("Перечисление.ВариантыМестаПроживания.ЖивутВУказанномРегионе"), "&relocation=living", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(ВариантПроживания = ПредопределенноеЗначение("Перечисление.ВариантыМестаПроживания.ЖивутВУказанномРегионеИлиГотовыПереехать"), "&relocation=living_or_relocation", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(ВариантПроживания = ПредопределенноеЗначение("Перечисление.ВариантыМестаПроживания.ЖивутВУказанномРегионеИГотовыКПереезду"), "&relocation=living_but_relocation", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(ВариантПроживания = ПредопределенноеЗначение("Перечисление.ВариантыМестаПроживания.НеЖивутВУказанномРегионеГотовыПереехать"), "&relocation=relocation", "");
	КонецЕсли;
	
	Если ИспользоватьМестоПроживания Тогда
		Для Каждого Город Из СписокГородов Цикл
			СтрокаЗапроса = СтрокаЗапроса + "&area=" + Город.ИдентификаторГородаХХ;	
		КонецЦикла;
	КонецЕсли;
	
	Если ЖелаемаяЗарплатаОт <> 0 Тогда
		СтрокаЗапроса = СтрокаЗапроса + "&salary_from=" + СтрЗаменить(Строка(ЖелаемаяЗарплатаОт), Символы.НПП, "");	
	КонецЕсли;
	
	Если ЖелаемаяЗарплатаДо <> 0 Тогда
		СтрокаЗапроса = СтрокаЗапроса + "&salary_to=" + СтрЗаменить(Строка(ЖелаемаяЗарплатаДо), Символы.НПП, "");	
	КонецЕсли;
	
	Если ЖелаемаяЗарплатаУказана Тогда
		СтрокаЗапроса = СтрокаЗапроса + "&label=only_with_salary";	
	КонецЕсли;
		
	Если НЕ СортироватьРезюмеПо.Пустая() Тогда
		СтрокаЗапроса = СтрокаЗапроса + ?(СортироватьРезюмеПо = ПредопределенноеЗначение("Перечисление.ВариантыСортировкиРезюме.ПоСоответствию"), "&order_by=relevance", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(СортироватьРезюмеПо = ПредопределенноеЗначение("Перечисление.ВариантыСортировкиРезюме.ПоДатеИзменения"), "&order_by=publication_time", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(СортироватьРезюмеПо = ПредопределенноеЗначение("Перечисление.ВариантыСортировкиРезюме.ПоУбываниюЗарплат"), "&order_by=salary_desc", "");
		СтрокаЗапроса = СтрокаЗапроса + ?(СортироватьРезюмеПо = ПредопределенноеЗначение("Перечисление.ВариантыСортировкиРезюме.ПоВозрастаниюЗарплаты"), "&order_by=salary_asc", "");
	КонецЕсли;
	
	Если ИспользоватьСтатусПоискаРаботы Тогда
		Для Каждого СтрокаТЗ Из СтатусПоискаРаботы Цикл
			СтрокаЗапроса = СтрокаЗапроса + ?(СтрокаТЗ.Статус = ПредопределенноеЗначение("Перечисление.СтатусыПоискаРаботы.АктивноИщетРаботу"), "&job_search_status=active_search", "");
			СтрокаЗапроса = СтрокаЗапроса + ?(СтрокаТЗ.Статус = ПредопределенноеЗначение("Перечисление.СтатусыПоискаРаботы.РассматриваетПредложения"), "&job_search_status=looking_for_offers", "");
			СтрокаЗапроса = СтрокаЗапроса + ?(СтрокаТЗ.Статус = ПредопределенноеЗначение("Перечисление.СтатусыПоискаРаботы.ПредложилиРаботуРешает"), "&job_search_status=has_job_offer", "");	
		КонецЦикла;
	КонецЕсли;

	Возврат СтрокаЗапроса; 
	
КонецФункции 

&НаСервереБезКонтекста
Функция НайтиДубльКандидатаПоРезюме(АдресРезюме)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Кандидаты.Код КАК Код
		|ИЗ
		|	Справочник.Кандидаты КАК Кандидаты
		|ГДЕ
		|	Кандидаты.СсылкаНаРезюме ПОДОБНО &СсылкаНаРезюме";
	
	Запрос.УстановитьПараметр("СсылкаНаРезюме", АдресРезюме);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РезультатПоиска = Новый Структура;
	Если РезультатЗапроса.Пустой() Тогда
		РезультатПоиска.Вставить("НайденДубль", Ложь);
		Возврат РезультатПоиска;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();	
	Выборка.Следующий();
	
	РезультатПоиска.Вставить("НайденДубль", Истина);
	РезультатПоиска.Вставить("КодКандидата", Выборка.Код);
	
	Возврат РезультатПоиска;

КонецФункции

&НаКлиенте
Процедура СозданиеЭлементаСправочникаКандидата(ОтветJSON)
	
	ТекущаСтрокаТЧ = Элементы.СписокНайденныхРезюме.ТекущиеДанные;
		
	ЗначенияРеквизитов = Новый Структура;
	ЗначенияРеквизитов.Вставить("ИсточникЗаполнения", "ПоискИПросмотрРезюмеХХ");
	ЗначенияРеквизитов.Вставить("ИсточникПривлечения", ПредопределенноеЗначение("Справочник.ИсточникиПривлечения.ХэдХантер"));
	ЗначенияРеквизитов.Вставить("СсылкаНаСкачиваниеРезюмеХХ", ТекущаСтрокаТЧ.СсылкаНаСкачиваниеРезюме);
	ЗначенияРеквизитов.Вставить("СсылкаНаРезюме", ТекущаСтрокаТЧ.АдресРезюме);
	
	ГородКандидата = ОтветJSON["area"];
	Если ГородКандидата <> Неопределено Тогда
		ГородКандидата = ОтветJSON["area"]["id"];	
	КонецЕсли;
	
	ЗначенияРеквизитов.Вставить("ГородПроживания", ГородКандидата);
	
	ЗначенияРеквизитов.Вставить("ДатаСозданияРезюме", ТекущаСтрокаТЧ.ДатаСозданияРезюме);
	ЗначенияРеквизитов.Вставить("ДатаПоследнегоОбновленияРезюме", ТекущаСтрокаТЧ.ПоследнееОбновление);
	ЗначенияРеквизитов.Вставить("ЖелаемаяДолжность", ОтветJSON["title"]);
	ЗначенияРеквизитов.Вставить("Возраст", ОтветJSON["age"]); 
	
	УровеньОбразования = ОтветJSON["education"]["level"];
	
	Если УровеньОбразования <> Неопределено Тогда  
		ЗначенияРеквизитов.Вставить("УровеньОбразованияКандидата", УровеньОбразования); 
	КонецЕсли;
		
	ПолКандидата = ОтветJSON["gender"]; 
	
	Если ПолКандидата <> Неопределено Тогда
		МужскойПол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Мужской");
		ЖенскийПол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Женский");
		ПолКандидата = ?(ОтветJSON["gender"]["id"] = "male", МужскойПол, ЖенскийПол);
		ЗначенияРеквизитов.Вставить("Пол", ПолКандидата); 	
	КонецЕсли;
	
	ОжидаемаяЗарплата = ОтветJSON["salary"];
	
	Если ОжидаемаяЗарплата <> Неопределено Тогда 
		ЗначенияРеквизитов.Вставить("ОжидаемаяЗарплата", ОтветJSON["salary"]["amount"]);
	КонецЕсли;   
	
	ОбщийОпытКандидата = ОтветJSON["total_experience"];
	
	Если ОбщийОпытКандидата <> Неопределено Тогда	
		ОбщийОпытРаботыВМесяцах = ОтветJSON["total_experience"]["months"];		
		Если ОбщийОпытРаботыВМесяцах <> 0 Тогда
			КоличествоОтработанныхЛет = Цел(ОбщийОпытРаботыВМесяцах / 12);
			КоличествоОтработанныхМесяцев = Строка(ОбщийОпытРаботыВМесяцах % 12);
		КонецЕсли;     
	КонецЕсли;
	ЗначенияРеквизитов.Вставить("ОбщийОпытВГодах", КоличествоОтработанныхЛет);
	ЗначенияРеквизитов.Вставить("ОбщийОпытВМесяцах", КоличествоОтработанныхМесяцев);
		
	ЗначенияРеквизитов.Вставить("ОпытРаботы", ОтветJSON["experience"]);	

	ДеньРождения = ОтветJSON["birth_date"];	
	Если ДеньРождения <> Неопределено Тогда
		ЗначенияРеквизитов.Вставить("ДатаРождения", ОбщегоНазначенияКлиент.ПолучитьФорматДатыХХ(ДеньРождения));	
	КонецЕсли;
	
	ЗначенияРеквизитов.Вставить("ЗнанияЯзыков", ОтветJSON["language"]);
		
	МетроКандидата = ОтветJSON["metro"];	
	Если МетроКандидата <> Неопределено Тогда
		ЗначенияРеквизитов.Вставить("СтанцияМетро", МетроКандидата["name"]);		
	КонецЕсли;
	
	ЗначенияРеквизитов.Вставить("ПодходящиеГрафикиРаботы", ОтветJSON["schedules"]);
	ЗначенияРеквизитов.Вставить("ПодходящиеТипыЗанятости", ОтветJSON["employments"]);

	ЗначенияРеквизитов.Вставить("ДополнительнаяИнформацияОСебе", ОтветJSON["skills"]);
	СписокКлючевыхНавыков = ОтветJSON["skill_set"];
	ЗначенияРеквизитов.Вставить("КлючевыеНавыки", СписокКлючевыхНавыков);		
		
	ПараметрыЗаполнения = Новый Структура("ЗначенияЗаполнения", ЗначенияРеквизитов);	
	ОткрытьФорму("Справочник.Кандидаты.ФормаОбъекта", ПараметрыЗаполнения);
			
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНаименованиеВакансии(Вакансия)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Вакансия.НаименованиеВакансии КАК НаименованиеВакансии
		|ИЗ
		|	Документ.Вакансия КАК Вакансия
		|ГДЕ
		|	Вакансия.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Вакансия);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();	
	Выборка.Следующий();

	Возврат Выборка.НаименованиеВакансии;	

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеГорода(ВыбранноеЗначение)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Города.Наименование КАК Наименование,
		|	Города.ИдентификаторГородаХХ КАК ИдентификаторГородаХХ
		|ИЗ
		|	Справочник.Города КАК Города
		|ГДЕ
		|	Города.Ссылка В(&МассивГородов)";
	
	Запрос.УстановитьПараметр("МассивГородов", ВыбранноеЗначение);	
	РезультатЗапроса = Запрос.Выполнить();	
	Выборка = РезультатЗапроса.Выбрать();
	
	МассивГородов = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		ДанныеГорода = Новый Структура("Наименование, ИдентификаторГорода", Выборка.Наименование, Выборка.ИдентификаторГородаХХ);
		МассивГородов.Добавить(ДанныеГорода);
	КонецЦикла;
	
	Возврат МассивГородов;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьНазваниеГруппыПодборГорода()
	
	КоличествоГородов = СписокГородов.Количество();
	Если КоличествоГородов > 1 Тогда
		Элементы.ГруппаПодборГорода.Заголовок = "Место проживания: " + СписокГородов.Получить(0).Город + " и еще " + КоличествоГородов;
	ИначеЕсли КоличествоГородов = 1 Тогда 
		Элементы.ГруппаПодборГорода.Заголовок = "Место проживания: " + СписокГородов.Получить(0).Город;
	ИначеЕсли КоличествоГородов = 0 Тогда
		Элементы.ГруппаПодборГорода.Заголовок = "Место проживания";
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНазваниеГруппыСтатусПоискаРаботы()

	КоличествоСтатусов = СтатусПоискаРаботы.Количество();
	Если КоличествоСтатусов > 1 Тогда
		Элементы.ГруппаСтатусПоискаРаботы.Заголовок = "Статус поиска работы: " + СтатусПоискаРаботы.Получить(0).Статус + " и еще " + КоличествоСтатусов;
	ИначеЕсли КоличествоСтатусов = 1 Тогда
		Элементы.ГруппаСтатусПоискаРаботы.Заголовок = "Статус поиска работы: " + СтатусПоискаРаботы.Получить(0).Статус;
	ИначеЕсли КоличествоСтатусов = 0 Тогда
		Элементы.ГруппаСтатусПоискаРаботы.Заголовок = "Статус поиска работы";	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
