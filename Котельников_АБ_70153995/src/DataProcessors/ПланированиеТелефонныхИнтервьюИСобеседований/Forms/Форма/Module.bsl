
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоМенеджер = ПользователиИнформационнойБазы.ТекущийПользователь().Роли.Содержит(Метаданные.Роли.МенеджерПоПодбору);
	ЭтоИнициатор = ПользователиИнформационнойБазы.ТекущийПользователь().Роли.Содержит(Метаданные.Роли.ИнициаторЗаявки);
	
	ПланировщикСобеседований.ТекущиеПериодыОтображения.Очистить();

	ПериодОтображения = "Неделя";
	ПланировщикСобеседований.ТекущиеПериодыОтображения.Добавить(НачалоНедели(ТекущаяДата()), КонецНедели(ТекущаяДата()));
	ШкалаВремени = ПланировщикСобеседований.ШкалаВремени.Элементы.Получить(0);
	ШкалаВремени.Единица = ТипЕдиницыШкалыВремени.Минута;
	ШкалаВремени.Кратность = 15;
	
	ПланировщикСобеседований.ЕдиницаПериодическогоВарианта = ТипЕдиницыШкалыВремени.Час;
	ПланировщикСобеседований.КратностьПериодическогоВарианта = 24;
	
	ПланировщикСобеседований.ОтступСНачалаПереносаШкалыВремени = 7;
	ПланировщикСобеседований.ОтступСКонцаПереносаШкалыВремени = 2;
	ПланировщикСобеседований.ВыравниватьГраницыЭлементовПоШкалеВремени = Ложь;
	
	ОбновитьПланировщикНаСервере(ЭтоМенеджер, ЭтоИнициатор);
	ИзменитьЗаголовокФормы(НачалоНедели(ТекущаяДата()), КонецНедели(ТекущаяДата()));
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОтображенияПриИзменении(Элемент)
	
	ПланировщикСобеседований.ТекущиеПериодыОтображения.Очистить();
	
	Если ПериодОтображения = "День" Тогда
		ПланировщикСобеседований.ТекущиеПериодыОтображения.Добавить(НачалоДня(ТекущаяДата()), КонецДня(ТекущаяДата()));
		ШкалаВремени = ПланировщикСобеседований.ШкалаВремени.Элементы.Получить(0);
		ШкалаВремени.Единица = ТипЕдиницыШкалыВремени.Минута;
		ШкалаВремени.Кратность = 10;
		ИзменитьЗаголовокФормы(НачалоДня(ТекущаяДата()), КонецДня(ТекущаяДата()));
	ИначеЕсли ПериодОтображения = "Неделя" Тогда 
		ПланировщикСобеседований.ТекущиеПериодыОтображения.Добавить(НачалоНедели(ТекущаяДата()), КонецНедели(ТекущаяДата()));
		ШкалаВремени = ПланировщикСобеседований.ШкалаВремени.Элементы.Получить(0);
		ШкалаВремени.Единица = ТипЕдиницыШкалыВремени.Минута;
		ШкалаВремени.Кратность = 15;
		ИзменитьЗаголовокФормы(НачалоНедели(ТекущаяДата()), КонецНедели(ТекущаяДата()));
	ИначеЕсли ПериодОтображения = "ДвеНедели" Тогда 
		ПланировщикСобеседований.ТекущиеПериодыОтображения.Добавить(НачалоНедели(ТекущаяДата()), КонецНедели(КонецНедели(ТекущаяДата()) + 1));
		ШкалаВремени = ПланировщикСобеседований.ШкалаВремени.Элементы.Получить(0);
		ШкалаВремени.Единица = ТипЕдиницыШкалыВремени.Час;
		ШкалаВремени.Кратность = 1;
		ИзменитьЗаголовокФормы(НачалоНедели(ТекущаяДата()), КонецНедели(КонецНедели(ТекущаяДата()) + 1));
	КонецЕсли;
	
	ОбновитьПланировщикНаСервере(ЭтоМенеджер, ЭтоИнициатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПланирощик(Команда)
	
	ОбновитьПланировщикНаСервере(ЭтоМенеджер, ЭтоИнициатор);	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПланировщикНаСервере(ЭтоМенеджер, ЭтоИнициатор)
	
	ПланировщикСобеседований.Элементы.Очистить();
		
	Если ЭтоМенеджер Тогда
		ТелефонныеИнтервью = ПолучитьЗапланированныеИнтервью();
		ПервичныеСобеседования = ПолучитьЗапланированныеПервичныеСобеседования();	
		Для Каждого Интервью Из ТелефонныеИнтервью Цикл
			НачалоИнтервью = Интервью.ДатаИнтервью + Интервью.Начало;
			ОкончаниеИнтервью = Интервью.ДатаИнтервью + Интервью.Начало + Интервью.Окончание; 
			Элемент = ПланировщикСобеседований.Элементы.Добавить(НачалоИнтервью, ОкончаниеИнтервью);
			Элемент.Текст = "Телефонное интервью (" + Интервью.Номер + ")";
			Элемент.Значение = Интервью.Ссылка;
			Элемент.ЦветФона = Новый Цвет(251, 237, 158);
		КонецЦикла;
		
		Для Каждого Собеседование Из ПервичныеСобеседования Цикл
			НачалоСобеседования = Собеседование.ДатаСобеседования + Собеседование.Начало;
			ОкончаниеСобеседования = Собеседование.ДатаСобеседования + Собеседование.Начало + Собеседование.Окончание; 
			Элемент = ПланировщикСобеседований.Элементы.Добавить(НачалоСобеседования, ОкончаниеСобеседования);
			Элемент.Текст = "Первичное собеседование (" + Собеседование.Номер + ")";
			Элемент.Значение = Собеседование.Ссылка;
		КонецЦикла;
	ИначеЕсли ЭтоИнициатор Тогда
		СобеседованияСИнициатором = ПолучитьЗапланированныеСобеседованияСИнициатором();
		
		Для Каждого Собеседование Из СобеседованияСИнициатором Цикл
			НачалоСобеседования = Собеседование.ДатаСобеседования + Собеседование.Начало;
			ОкончаниеСобеседования = Собеседование.ДатаСобеседования + Собеседование.Начало + Собеседование.Окончание; 
			Элемент = ПланировщикСобеседований.Элементы.Добавить(НачалоСобеседования, ОкончаниеСобеседования);
			Элемент.Текст = "Собеседование (" + Собеседование.Номер + ")";
			Элемент.Значение = Собеседование.Ссылка;
		КонецЦикла;
	КонецЕсли;
		
	//НачалоПериодаПланировщика = ПланировщикСобеседований.ТекущиеПериодыОтображения[0].Начало;
	//КонецПериодаПланировщика = ПланировщикСобеседований.ТекущиеПериодыОтображения[0].Конец;	
	//
	//Пока НачалоПериодаПланировщика < КонецПериодаПланировщика Цикл
	//	Если ДеньНедели(НачалоПериодаПланировщика) = 6 ИЛИ ДеньНедели(НачалоПериодаПланировщика) = 7 Тогда
	//		НовыйЭлемент = ПланировщикСобеседований.Элементы.Добавить(НачалоДня(НачалоПериодаПланировщика), КонецДня(НачалоПериодаПланировщика));
	//		НовыйЭлемент.Текст = "Выходной";
	//		НовыйЭлемент.ЦветФона = Новый Цвет(255, 127, 80);
	//	КонецЕсли;
	//	
	//	НачалоПериодаПланировщика = НачалоПериодаПланировщика + 60 * 60 * 24;
	//КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗапланированныеИнтервью()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроведениеТелефонногоИнтервью.Ссылка КАК Ссылка,
		|	ПроведениеТелефонногоИнтервью.Номер КАК Номер,
		|	ПроведениеТелефонногоИнтервью.ДатаПроведенияИнтервью КАК ДатаПроведенияИнтервью,
		|	ПроведениеТелефонногоИнтервью.ВремяНачала КАК ВремяНачала,
		|	ПроведениеТелефонногоИнтервью.ВремяОкончания КАК ВремяОкончания
		|ИЗ
		|	Документ.ПроведениеТелефонногоИнтервью КАК ПроведениеТелефонногоИнтервью
		|ГДЕ
		|	ПроведениеТелефонногоИнтервью.Ответственный = &Ответственный
		|	И ПроведениеТелефонногоИнтервью.СтатусИнтервью = &СтатусИнтервью
		|	И ПроведениеТелефонногоИнтервью.ПометкаУдаления = &ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Ответственный", ПараметрыСеанса.ТекущийПользователь.Сотрудник);
	Запрос.УстановитьПараметр("СтатусИнтервью", Перечисления.СтатусыТелефонногоИнтервью.Запланировано);
	Запрос.УстановитьПараметр("ПометкаУдаления", Ложь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ТелефонныеИнтервью = Новый Массив;	
	Пока Выборка.Следующий() Цикл	
		Интервью = Новый Структура;
		Интервью.Вставить("Ссылка", Выборка.Ссылка);
		Интервью.Вставить("Номер", Выборка.Номер);
		Интервью.Вставить("ДатаИнтервью", Выборка.ДатаПроведенияИнтервью);
		Интервью.Вставить("Начало", Час(Выборка.ВремяНачала) * 60 * 60 + Минута(Выборка.ВремяНачала) * 60);
		ДлительностьЧасы = (Час(Выборка.ВремяОкончания) - Час(Выборка.ВремяНачала)) * 60 * 60;
		ДлительностьМинуты = (Минута(Выборка.ВремяОкончания) - Минута(Выборка.ВремяНачала)) * 60; 
		
		Интервью.Вставить("Окончание", ДлительностьЧасы + ДлительностьМинуты);	
		ТелефонныеИнтервью.Добавить(Интервью);	
	КонецЦикла;
	
	Возврат ТелефонныеИнтервью;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьЗапланированныеПервичныеСобеседования()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроведениеСобеседования.Ссылка КАК Ссылка,
		|	ПроведениеСобеседования.Номер КАК Номер,
		|	ПроведениеСобеседования.ДатаСобеседования КАК ДатаСобеседования,
		|	ПроведениеСобеседования.ВремяНачала КАК ВремяНачала,
		|	ПроведениеСобеседования.ВремяОкончания КАК ВремяОкончания
		|ИЗ
		|	Документ.ПроведениеСобеседования КАК ПроведениеСобеседования
		|ГДЕ
		|	ПроведениеСобеседования.СтатусСобеседования = &СтатусСобеседования
		|	И ПроведениеСобеседования.ПометкаУдаления = &ПометкаУдаления
		|	И ПроведениеСобеседования.АвторДокумента = &АвторДокумента
		|	И ПроведениеСобеседования.ТипСобеседования = &ТипСобеседования";
	
	Запрос.УстановитьПараметр("АвторДокумента", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("ПометкаУдаления", Ложь);
	Запрос.УстановитьПараметр("СтатусСобеседования", Перечисления.СтатусыСобеседований.Запланировано);
	Запрос.УстановитьПараметр("ТипСобеседования", Перечисления.ТипыСобеседований.ПервичноеСобеседование);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	ПервичныеСобеседования = Новый Массив;	
	Пока Выборка.Следующий() Цикл	
		Собеседование = Новый Структура;
		Собеседование.Вставить("Ссылка", Выборка.Ссылка);
		Собеседование.Вставить("Номер", Выборка.Номер);
		Собеседование.Вставить("ДатаСобеседования", Выборка.ДатаСобеседования);
		Собеседование.Вставить("Начало", Час(Выборка.ВремяНачала) * 60 * 60 + Минута(Выборка.ВремяНачала) * 60);
		ДлительностьЧасы = (Час(Выборка.ВремяОкончания) - Час(Выборка.ВремяНачала)) * 60 * 60;
		ДлительностьМинуты = (Минута(Выборка.ВремяОкончания) - Минута(Выборка.ВремяНачала)) * 60; 
		
		Собеседование.Вставить("Окончание", ДлительностьЧасы + ДлительностьМинуты);	
		ПервичныеСобеседования.Добавить(Собеседование);	
	КонецЦикла;
	
	Возврат ПервичныеСобеседования;
	
КонецФункции

&НаКлиенте
Процедура УстановитьДату(Команда)
	
	ОчиститьСообщения();
	
	Если ДеньПланировщика = '00010101' Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Не задана дата", "ДеньПланировщика");
		Возврат;	
	КонецЕсли;
	
	ПериодОтображения = Неопределено;
	
	ПланировщикСобеседований.ТекущиеПериодыОтображения.Очистить();
	ПланировщикСобеседований.ТекущиеПериодыОтображения.Добавить(НачалоДня(ДеньПланировщика), КонецДня(ДеньПланировщика));
	ШкалаВремени = ПланировщикСобеседований.ШкалаВремени.Элементы.Получить(0);
	ШкалаВремени.Единица = ТипЕдиницыШкалыВремени.Минута;
	ШкалаВремени.Кратность = 10;
	ИзменитьЗаголовокФормы(НачалоДня(ДеньПланировщика), КонецДня(ДеньПланировщика));
	ОбновитьПланировщикНаСервере(ЭтоМенеджер, ЭтоИнициатор);
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗапланированныеСобеседованияСИнициатором()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроведениеСобеседования.Ссылка КАК Ссылка,
		|	ПроведениеСобеседования.Номер КАК Номер,
		|	ПроведениеСобеседования.ДатаСобеседования КАК ДатаСобеседования,
		|	ПроведениеСобеседования.ВремяНачала КАК ВремяНачала,
		|	ПроведениеСобеседования.ВремяОкончания КАК ВремяОкончания
		|ИЗ
		|	Документ.ПроведениеСобеседования КАК ПроведениеСобеседования
		|ГДЕ
		|	ПроведениеСобеседования.СтатусСобеседования = &СтатусСобеседования
		|	И ПроведениеСобеседования.ТипСобеседования = &ТипСобеседования
		|	И ПроведениеСобеседования.АвторДокумента = &АвторДокумента
		|	И ПроведениеСобеседования.ПометкаУдаления = &ПометкаУдаления";
	
	Запрос.УстановитьПараметр("АвторДокумента", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("ПометкаУдаления", Ложь);
	Запрос.УстановитьПараметр("СтатусСобеседования", Перечисления.СтатусыСобеседований.Запланировано);
	Запрос.УстановитьПараметр("ТипСобеседования", Перечисления.ТипыСобеседований.СобеседованиеСИнициатором);
	
	РезультатЗапроса = Запрос.Выполнить();	
	Выборка = РезультатЗапроса.Выбрать();
	
	СобеседованияСИнициатором = Новый Массив;	
	Пока Выборка.Следующий() Цикл
		Собеседование = Новый Структура;
		Собеседование.Вставить("Ссылка", Выборка.Ссылка);
		Собеседование.Вставить("Номер", Выборка.Номер);
		Собеседование.Вставить("ДатаСобеседования", Выборка.ДатаСобеседования);
		Собеседование.Вставить("Начало", Час(Выборка.ВремяНачала) * 60 * 60 + Минута(Выборка.ВремяНачала) * 60);
		ДлительностьЧасы = (Час(Выборка.ВремяОкончания) - Час(Выборка.ВремяНачала)) * 60 * 60;
		ДлительностьМинуты = (Минута(Выборка.ВремяОкончания) - Минута(Выборка.ВремяНачала)) * 60; 
		
		Собеседование.Вставить("Окончание", ДлительностьЧасы + ДлительностьМинуты);	
		СобеседованияСИнициатором.Добавить(Собеседование);			
	КонецЦикла;
	
	Возврат СобеседованияСИнициатором; 

КонецФункции

&НаКлиенте
Асинх Процедура ПланировщихСобеседованийПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЭтоМенеджер Тогда
		
		Если Вакансия.Пустая() Тогда
			ОбщегоНазначенияКлиент.ВывестиСообщение("Выберите вакансию", "Вакансия");
			Возврат;
		КонецЕсли;
		
		Если Кандидат.Пустая() Тогда
			ОбщегоНазначенияКлиент.ВывестиСообщение("Выберите кандидата", "Кандидат");
			Возврат;
		КонецЕсли;
	
		КнопкиВопроса = Новый СписокЗначений;
		КнопкиВопроса.Добавить("ТелефонноеИнтервью", "Телефонное интервью");
		КнопкиВопроса.Добавить("ПервичноеСобеседование", "Первичное собеседование");
		КнопкиВопроса.Добавить("Отмена");
		
		ОтветПользователя = Ждать ВопросАсинх("Создать телефонное интервью или первичное собеседование?", КнопкиВопроса,, "Отмена", "Какой документ создать?");
		
		Если ОтветПользователя = "ТелефонноеИнтервью" Тогда
			
			ДанныеКандидата = ПолучитьДанныеКандидата(Кандидат);
			
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("Вакансия", Вакансия); 
			ЗначенияЗаполнения.Вставить("ДатаПроведенияИнтервью", Начало);
			НачалоЧасы = Час(Начало) * 60 * 60;
			НачалоМинуты = Минута(Начало) * 60;
			ЗначенияЗаполнения.Вставить("ВремяНачала", '00010101' + НачалоЧасы + НачалоМинуты);
			
			КонецЧасы = Час(Конец) * 60 * 60;
			КонецМинуты = Минута(Конец) * 60; 
			ЗначенияЗаполнения.Вставить("ВремяОкончания", '00010101' + КонецЧасы + КонецМинуты);
			ЗначенияЗаполнения.Вставить("Кандидат", Кандидат);
			ЗначенияЗаполнения.Вставить("ТелефонКандидата", ДанныеКандидата.Телефон);
			ЗначенияЗаполнения.Вставить("ЭмаилКандидата", ДанныеКандидата.Эмаил);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			
			ОткрытьФорму("Документ.ПроведениеТелефонногоИнтервью.ФормаОбъекта", ПараметрыФормы);
			
		ИначеЕсли ОтветПользователя = "ПервичноеСобеседование" Тогда
					
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("Вакансия", Вакансия); 
			ЗначенияЗаполнения.Вставить("ДатаСобеседования", Начало);
			НачалоЧасы = Час(Начало) * 60 * 60;
			НачалоМинуты = Минута(Начало) * 60;
			ЗначенияЗаполнения.Вставить("ВремяНачала", '00010101' + НачалоЧасы + НачалоМинуты);
			
			КонецЧасы = Час(Конец) * 60 * 60;
			КонецМинуты = Минута(Конец) * 60; 
			ЗначенияЗаполнения.Вставить("ВремяОкончания", '00010101' + КонецЧасы + КонецМинуты);
			
			ЗначенияЗаполнения.Вставить("Кандидат", Кандидат);
				
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			
			ОткрытьФорму("Документ.ПроведениеСобеседования.ФормаОбъекта", ПараметрыФормы);
		КонецЕсли;
	ИначеЕсли ЭтоИнициатор Тогда
		
		Если Вакансия.Пустая() Тогда
			ОбщегоНазначенияКлиент.ВывестиСообщение("Выберите вакансию", "Вакансия");
			Возврат;
		КонецЕсли;
		
		КнопкиВопроса = Новый СписокЗначений;
		КнопкиВопроса.Добавить("Создать");
		КнопкиВопроса.Добавить("Отмена");
		
		ОтветПользователя = Ждать ВопросАсинх("Создать новый документ ""Собеседование?""", КнопкиВопроса,, "Отмена", "Создать документ?");
		
		Если ОтветПользователя = "Создать" Тогда
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("Вакансия", Вакансия); 
			ЗначенияЗаполнения.Вставить("ДатаСобеседования", Начало);
			НачалоЧасы = Час(Начало) * 60 * 60;
			НачалоМинуты = Минута(Начало) * 60;
			ЗначенияЗаполнения.Вставить("ВремяНачала", '00010101' + НачалоЧасы + НачалоМинуты);
			
			КонецЧасы = Час(Конец) * 60 * 60;
			КонецМинуты = Минута(Конец) * 60; 
			ЗначенияЗаполнения.Вставить("ВремяОкончания", '00010101' + КонецЧасы + КонецМинуты);
			ЗначенияЗаполнения.Вставить("Кандидат", Кандидат);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			
			ОткрытьФорму("Документ.ПроведениеСобеседования.ФормаОбъекта", ПараметрыФормы);				
		КонецЕсли;		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПланировщихСобеседованийПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)
	
	ВыделенныйЭлемент = Элемент.ВыделенныеЭлементы[0].Значение; 
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ВыделенныйЭлемент);
	
	Если ТипЗнч(ВыделенныйЭлемент) = Тип("ДокументСсылка.ПроведениеТелефонногоИнтервью") Тогда
		ОткрытьФорму("Документ.ПроведениеТелефонногоИнтервью.ФормаОбъекта", ПараметрыФормы);
	ИначеЕсли ТипЗнч(ВыделенныйЭлемент) = Тип("ДокументСсылка.ПроведениеСобеседования") Тогда
		ОткрытьФорму("Документ.ПроведениеСобеседования.ФормаОбъекта", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьЗаголовокФормы(Начало, Конец)

	Если ЭтоМенеджер Тогда
		Заголовок = "Запланированные телефонные интервью и первичные собеседования " + ПредставлениеПериода(Начало, Конец);	
	ИначеЕсли ЭтоИнициатор Тогда
		Заголовок = "Запланированные собеседования " + ПредставлениеПериода(Начало, Конец);
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЭтотОбъект.ВладелецФормы <> Неопределено Тогда
		Вакансия = ЭтотОбъект.ВладелецФормы.Объект.СсылкаНаВакансию;
		Кандидат = ЭтотОбъект.ВладелецФормы.Объект.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеКандидата(Кандидат)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Кандидаты.Телефон КАК Телефон,
		|	Кандидаты.Эмаил КАК Эмаил
		|ИЗ
		|	Справочник.Кандидаты КАК Кандидаты
		|ГДЕ
		|	Кандидаты.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Кандидат);	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
		
	ДанныеКандидата = Новый Структура("Телефон, Эмаил", Выборка.Телефон, Выборка.Эмаил);
	
	Возврат ДанныеКандидата;
	
КонецФункции



