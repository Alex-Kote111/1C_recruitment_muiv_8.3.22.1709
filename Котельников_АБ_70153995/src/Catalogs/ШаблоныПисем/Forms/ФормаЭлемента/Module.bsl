
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если Объект.Ссылка.Пустая() Тогда
		ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
		Объект.Пользователь = ТекущийПользователь; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ДекорацияИнструкция.Заголовок = "Разрешено указывать следующие параметры в квадратных скобках:
												|[Кандидат] - будет подставлено ФИО кандидата
												|[Вакансия] - будет подставлено наименование вакансии
												|[Отправитель] - будет подставлено ваше ФИО
												|[Должность] - будет подставлена ваша должность
												|[Организация] - будет подставлено наименование организации
												|[Адрес] - будет подставлен адрес организации
												|[ДатаСобеседования] - будет подставлена дата собеседования]
												|[ВремяНачала] - будет подставлено время начала собеседования";
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьТестовоеСообщениеСебе(Команда)
	
	ОчиститьСообщения();
	
	ДанныеДляОтправки = ПолучитьДанныеДляОтправки();
	
	Если ПустаяСтрока(ДанныеДляОтправки.Почта) ИЛИ ПустаяСтрока(ДанныеДляОтправки.Пароль) Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Не заполнены данные почты пользователя. Отправка невозможна");
		Возврат;
	КонецЕсли;
		
	Профиль = Новый ИнтернетПочтовыйПрофиль;
	Профиль.АдресСервераSMTP = "smtp.mail.ru";
	Профиль.ПользовательSMTP = ДанныеДляОтправки.Почта;
	Профиль.ПарольSMTP = ДанныеДляОтправки.Пароль;
	Профиль.ИспользоватьSSLSMTP = Истина;
	Профиль.ПортSMTP = 465;     

	Письмо = Новый ИнтернетПочтовоеСообщение;
	
	ТекстПисьма = ПодставитьЗначенияВПисьмо(Объект.ТекстПисьма, ДанныеДляОтправки);
	
	Текст = Письмо.Тексты.Добавить(ТекстПисьма);
	Текст.ТипТекста = ТипТекстаПочтовогоСообщения.ПростойТекст;
	Письмо.Тема = Объект.Тема; 
	Письмо.Отправитель = ДанныеДляОтправки.Почта;
	Письмо.ИмяОтправителя = Объект.ИмяОтправителя;
	
	Если ПустаяСтрока(Объект.ИмяОтправителя) Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Поле ""Имя отправителя"" не заполнено", "Объект.ИмяОтправителя");
		Возврат;
	КонецЕсли; 
	
	Если ПустаяСтрока(Объект.Тема) Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Поле ""Тема"" не заполнено", "Объект.Тема");
		Возврат;
	КонецЕсли;
	
	Письмо.Получатели.Добавить(ДанныеДляОтправки.Почта);
	Почта = Новый ИнтернетПочта;	
	Попытка
		Почта.Подключиться(Профиль);
	Исключение
		ОбщегоНазначенияКлиент.ВывестиСообщение("Не удалось подключиться к серверу");
		ОбщегоНазначенияКлиент.ВывестиСообщение(ОписаниеОшибки());
	КонецПопытки;
	
	Попытка
		Почта.Послать(Письмо); 
		ОбщегоНазначенияКлиент.ВывестиСообщение("Письмо отправлено");
	Исключение
		ОбщегоНазначенияКлиент.ВывестиСообщение("Не удалось отправить письмо");
		ОбщегоНазначенияКлиент.ВывестиСообщение(ОписаниеОшибки());
	КонецПопытки;
	
	Почта.Отключиться();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеДляОтправки()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пользователи.ПочтаПользователяЭмаил КАК ПочтаПользователяЭмаил,
		|	Пользователи.ПарольПриложенияЭмаил КАК ПарольПриложенияЭмаил,
		|	Пользователи.Сотрудник.Должность КАК СотрудникДолжность,
		|	Пользователи.Сотрудник.Наименование КАК СотрудникНаименование
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ПараметрыСеанса.ТекущийПользователь);	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();	
	Выборка.Следующий();
	
	НаименованиеОрганизации = Константы.НаименованиеОрганизации.Получить();
	АдресОрганизации = Константы.АдресОрганизации.Получить();
	
    ДанныеДляОтправки = Новый Структура;
	ДанныеДляОтправки.Вставить("Почта", Выборка.ПочтаПользователяЭмаил);
	ДанныеДляОтправки.Вставить("Пароль", Выборка.ПарольПриложенияЭмаил);
	ДанныеДляОтправки.Вставить("ИмяСотрудника", Выборка.СотрудникНаименование);
	ДанныеДляОтправки.Вставить("Должность", Выборка.СотрудникДолжность);
	ДанныеДляОтправки.Вставить("НаименованиеОрганизации", НаименованиеОрганизации);
	ДанныеДляОтправки.Вставить("АдресОрганизации", АдресОрганизации);
	
	Возврат ДанныеДляОтправки; 

КонецФункции

&НаКлиенте
Функция ПодставитьЗначенияВПисьмо(Знач ТекстПисьма, ДанныеДляОтправки)
	
	ТекстПисьма = СтрЗаменить(ТекстПисьма, "[Кандидат]", "Иванов Иван Иванович");
	ТекстПисьма = СтрЗаменить(ТекстПисьма, "[Вакансия]", "Старший менеджер");
	ТекстПисьма = СтрЗаменить(ТекстПисьма, "[Отправитель]", ДанныеДляОтправки.ИмяСотрудника);
	ТекстПисьма = СтрЗаменить(ТекстПисьма, "[Должность]", ДанныеДляОтправки.Должность);
	ТекстПисьма = СтрЗаменить(ТекстПисьма, "[Организация]", ДанныеДляОтправки.НаименованиеОрганизации);
    ТекстПисьма = СтрЗаменить(ТекстПисьма, "[Адрес]", ДанныеДляОтправки.АдресОрганизации); 
	ТекстПисьма = СтрЗаменить(ТекстПисьма, "[ДатаСобеседования]", "3 мая 2025 г.");
    ТекстПисьма = СтрЗаменить(ТекстПисьма, "[ВремяНачала]", "10:00"); 
	
	Возврат ТекстПисьма;

КонецФункции
