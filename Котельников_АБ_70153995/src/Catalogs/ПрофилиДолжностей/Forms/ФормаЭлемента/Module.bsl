
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		ДанныеРуководителя = ПолучитьДанныеРуководителя();
		Объект.Подразделение = ДанныеРуководителя.Подразделение;
		Объект.РуководительПодразделения = ДанныеРуководителя.Сотрудник;
		ДобавитьЯзыкПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеРуководителя()

	ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пользователи.Сотрудник КАК Сотрудник,
		|	Пользователи.Сотрудник.Подразделение КАК Подразделение
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ТекущийПользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();

	Возврат Выборка;
	
КонецФункции

&НаСервере
Процедура ДобавитьЯзыкПоУмолчанию()

	СтрокаТЧ = Объект.ЗнанияЯзыков.Добавить();
	СтрокаТЧ.Язык = Справочники.Языки.РусскийЯзык;
	СтрокаТЧ.УровеньВладения = Перечисления.УровниЗнанийЯзыков.Родной;

КонецПроцедуры

&НаКлиенте
Процедура ДолжностьПриИзменении(Элемент)
	
	Если НЕ (Объект.Подразделение.Пустая() ИЛИ Объект.Должность.Пустая()) Тогда
		Зарплата = ПолучитьЗарплату(Объект.Подразделение, Объект.Должность);
		Объект.ОплатаТрудаОт = Зарплата.ЗарплатаОт;
		Объект.ОплатаТрудаДо = Зарплата.ЗарплатаДо;
		Объект.ДоВычетаНалога = Зарплата.ДоВычетаНалога;
	КонецЕсли;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗарплату(Подразделение, Должность)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗарплатыПоДолжностямСрезПоследних.ЗарплатаОт КАК ЗарплатаОт,
		|	ЗарплатыПоДолжностямСрезПоследних.ЗарплатаДо КАК ЗарплатаДо,
		|	ЗарплатыПоДолжностямСрезПоследних.ДоВычетаНалога КАК ДоВычетаНалога
		|ИЗ
		|	РегистрСведений.ЗарплатыПоДолжностям.СрезПоследних(
		|			,
		|			Подразделение = &Подразделение
		|				И Должность = &Должность) КАК ЗарплатыПоДолжностямСрезПоследних";
	
	Запрос.УстановитьПараметр("Должность", Должность);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	РезультатЗапроса = Запрос.Выполнить();	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ДанныеОЗарплате = Новый Структура;
	ДанныеОЗарплате.Вставить("ЗарплатаОт", Выборка.ЗарплатаОт);
	ДанныеОЗарплате.Вставить("ЗарплатаДо", Выборка.ЗарплатаДо);
	ДанныеОЗарплате.Вставить("ДоВычетаНалога", Выборка.ДоВычетаНалога);
	
	Возврат ДанныеОЗарплате;
	
КонецФункции

