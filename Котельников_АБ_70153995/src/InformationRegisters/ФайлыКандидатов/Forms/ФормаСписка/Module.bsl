
&НаКлиенте
Асинх Процедура ДобавитьФайл(Команда)
	
	ОчиститьСообщения();
	
	Если ЭтотОбъект.Окно.Содержимое.Количество() <> 2 Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Добавлять файлы кандидатам можно только из элемента справочника");
		Возврат;
	КонецЕсли;
	
	СсылкаНаКандидата = ЭтотОбъект.Окно.Содержимое[0].Объект.Ссылка;

	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов();
	ПараметрыДиалога.Заголовок = "Выберите файл";
	ПараметрыДиалога.Фильтр = "Файл | *.pdf; *.doc; *docx; *.rtf";
	
	Результат = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога, УникальныйИдентификатор); 
	
	Если Результат = Неопределено Тогда		
		Возврат;
	КонецЕсли; 
	
	ДанныеФайла = Новый Структура;
	ДанныеФайла.Вставить("Кандидат", СсылкаНаКандидата);
	ДанныеФайла.Вставить("Наименование", Результат.СсылкаНаФайл.Файл.ИмяБезРасширения);
	ДанныеФайла.Вставить("ИмяФайлаБезРасширения", Результат.СсылкаНаФайл.Файл.ИмяБезРасширения);
	ДанныеФайла.Вставить("РасширениеФайла", Результат.СсылкаНаФайл.Расширение);
	
	РазмерФайлаВБайтах = Ждать Результат.СсылкаНаФайл.Файл.РазмерАсинх();
	РазмерФайлаВКилобайтах = Окр(РазмерФайлаВБайтах / 1024, 1); 
	
	ДанныеФайла.Вставить("РазмерФайла", РазмерФайлаВКилобайтах);   
	ДанныеФайла.Вставить("АдресДанных", Результат.Адрес);
		
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ДанныеФайла);
	
	ОткрытьФорму("РегистрСведений.ФайлыКандидатов.ФормаЗаписи", ПараметрыФормы, ЭтотОбъект);
		
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьФайл(Команда) 
	
	ОчиститьСообщения();
	
	СтрокаТЧ = Элементы.Список.ТекущаяСтрока;
	
	Если СтрокаТЧ = Неопределено Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Не выбран файл или список записей пуст");
		Возврат;
	КонецЕсли;
	
	ДанныеФайлаСтруктура = ПолучитьДанныеФайла(СтрокаТЧ);	
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайлаСтруктура.АдресДанных); 
	
	ЗаписатьФайлНаОсновеДанных(ДвоичныеДанныеФайла, ДанныеФайлаСтруктура);
	
КонецПроцедуры 


&НаСервереБезКонтекста
Функция ПолучитьДанныеФайла(СсылкаНаФайл)
		
	ЗаписьРегистра = РегистрыСведений.ФайлыКандидатов.СоздатьМенеджерЗаписи();
	ЗаписьРегистра.Кандидат = СсылкаНаФайл.Кандидат;
	ЗаписьРегистра.Наименование = СсылкаНаФайл.Наименование;
	ЗаписьРегистра.ИмяФайлаБезРасширения = СсылкаНаФайл.ИмяФайлаБезРасширения;
	ЗаписьРегистра.РасширениеФайла = СсылкаНаФайл.РасширениеФайла; 
	ЗаписьРегистра.РазмерФайла = СсылкаНаФайл.РазмерФайла;
	ЗаписьРегистра.Прочитать();
		
	ДанныеФайла = Новый Структура;
	ДанныеФайла.Вставить("Наименование", ЗаписьРегистра.Наименование);
	ДанныеФайла.Вставить("ИмяФайлаБезРасширения", ЗаписьРегистра.ИмяФайлаБезРасширения);
	ДанныеФайла.Вставить("Расширение", ЗаписьРегистра.РасширениеФайла);
	
	ДвоичныеДанные = ЗаписьРегистра.ФайлКандидата.Получить();
	Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные);	
	ДанныеФайла.Вставить("АдресДанных", Адрес);

	Возврат ДанныеФайла; 
	
КонецФункции 


&НаКлиенте
Асинх Процедура ЗаписатьФайлНаОсновеДанных(ДвоичныеДанныеФайла, ДанныеФайлаСтруктура) 
	
	НаименованиеФайла = ДанныеФайлаСтруктура.Наименование;
	Расширение = ДанныеФайлаСтруктура.Расширение;
	Каталог = Ждать КаталогВременныхФайловАсинх();
	
	ПолныйПутьФайла = Каталог + "candidate_files\";
	
	Если НЕ ПустаяСтрока(НаименованиеФайла) Тогда
		ПолныйПутьФайла = ПолныйПутьФайла + НаименованиеФайла + Расширение;
	Иначе
		ПолныйПутьФайла = ПолныйПутьФайла + ДанныеФайлаСтруктура.ИмяФайлаБезРасширения + Расширение;	
	КонецЕсли;  
	
	Ждать ДвоичныеДанныеФайла.ЗаписатьАсинх(ПолныйПутьФайла);	
	Ждать ЗапуститьПриложениеАсинх(ПолныйПутьФайла);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПрочитатьПДФФайл(Команда)
	
	ОчиститьСообщения();
	
	Если ЭтотОбъект.Окно.Содержимое.Количество() <> 2 Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Читать pdf файл можно только из элемента справочника");
		Возврат;
	КонецЕсли;

	
	СтрокаТЧ = Элементы.Список.ТекущаяСтрока;
	
	Если СтрокаТЧ = Неопределено Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Не выбран файл или список записей пуст");
		Возврат;
	КонецЕсли;
	
	Если Элементы.Список.ТекущиеДанные.РасширениеФайла <> ".pdf" Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("У файла не расширение "".pdf""");
		Возврат;	
	КонецЕсли;
	
	ДанныеФайлаСтруктура = ПолучитьДанныеФайла(СтрокаТЧ);	
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайлаСтруктура.АдресДанных);
	
	НаименованиеФайла = ДанныеФайлаСтруктура.Наименование;
	Расширение = ДанныеФайлаСтруктура.Расширение;
	Каталог = Ждать КаталогВременныхФайловАсинх();
	
	ПолныйПутьФайла = Каталог + "candidate_files\";
	
	Если НЕ ПустаяСтрока(НаименованиеФайла) Тогда
		ПолныйПутьФайла = ПолныйПутьФайла + НаименованиеФайла + Расширение;
	Иначе
		ПолныйПутьФайла = ПолныйПутьФайла + ДанныеФайлаСтруктура.ИмяФайлаБезРасширения + Расширение;	
	КонецЕсли; 
	
	Ждать ДвоичныеДанныеФайла.ЗаписатьАсинх(ПолныйПутьФайла);
	
	ДокументPDF = Новый ДокументPDF();
	Ждать ДокументPDF.ПрочитатьАсинх(ПолныйПутьФайла);
	
	ДокументPDF.Показать("Резюме кандидата - " + ЭтотОбъект.Окно.Содержимое[0].Объект.ЖелаемаяДолжность)
	
КонецПроцедуры



