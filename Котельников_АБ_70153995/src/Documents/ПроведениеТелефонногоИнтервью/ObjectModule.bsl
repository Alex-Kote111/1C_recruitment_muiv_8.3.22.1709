
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РегистрацияКандидатов") Тогда
		Вакансия = ДанныеЗаполнения.Вакансия;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	ЗаписатьДвиженияКоличествоКандидатовПоЭтапам();
	ЗаписатьДвиженияСтатусыКандидатов();

КонецПроцедуры

Процедура ЗаписатьДвиженияКоличествоКандидатовПоЭтапам()

	// регистр накопления КоличествоКандидатовПоЭтапам Расход
	Движения.КоличествоКандидатовПоЭтапам.Записывать = Истина;
	Движение = Движения.КоличествоКандидатовПоЭтапам.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата;
	Движение.Вакансия = Вакансия;
	Движение.Кандидат = Кандидат;
	Движение.Этап = Перечисления.ЭтапыПодбора.ПросмотрРезюме;
	Движение.ЭтапПройден = Истина;
	Движение.ДатаИзмененияЭтапа = Дата;
	Движение.Количество = 1;

	// регистр накопления КоличествоКандидатовПоЭтапам Приход
	Движения.КоличествоКандидатовПоЭтапам.Записывать = Истина;
	Движение = Движения.КоличествоКандидатовПоЭтапам.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Вакансия = Вакансия;
	Движение.Кандидат = Кандидат;
	Движение.Этап = Перечисления.ЭтапыПодбора.ТелефонноеИнтервью;
	Если СтатусИнтервью = Перечисления.СтатусыТелефонногоИнтервью.Запланировано ИЛИ СтатусИнтервью = Перечисления.СтатусыТелефонногоИнтервью.НеСостоялось Тогда
		Движение.ЭтапПройден = Ложь;
	ИначеЕсли СтатусИнтервью = Перечисления.СтатусыТелефонногоИнтервью.Состоялось Тогда
		Движение.ЭтапПройден = Истина;
	КонецЕсли;	
	Движение.ДатаИзмененияЭтапа = Дата;
	Движение.Количество = 1;	

КонецПроцедуры

Процедура ЗаписатьДвиженияСтатусыКандидатов()

	// регистр сведений СтатусыКандидатов
	Движения.СтатусыКандидатов.Записывать = Истина;
	Движение = Движения.СтатусыКандидатов.Добавить();
	Движение.Период = Дата;
	Движение.Вакансия = Вакансия;
	Движение.Кандидат = Кандидат;
	Если СтатусИнтервью = Перечисления.СтатусыТелефонногоИнтервью.Запланировано ИЛИ СтатусИнтервью = Перечисления.СтатусыТелефонногоИнтервью.НеСостоялось Тогда
		Движение.ЭтапПройден = Ложь;
	ИначеЕсли СтатусИнтервью = Перечисления.СтатусыТелефонногоИнтервью.Состоялось Тогда
		Движение.ЭтапПройден = Истина;
	КонецЕсли;	
	Движение.Статус = Перечисления.ЭтапыПодбора.ТелефонноеИнтервью;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если СтатусИнтервью = Перечисления.СтатусыТелефонногоИнтервью.Запланировано Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Результат"));
	КонецЕсли;
	
КонецПроцедуры
