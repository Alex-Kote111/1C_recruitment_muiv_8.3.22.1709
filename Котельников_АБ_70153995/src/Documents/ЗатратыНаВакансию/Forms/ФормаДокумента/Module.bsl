
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда	
		ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
		Объект.Ответственный = ТекущийПользователь.Сотрудник;
		Объект.АвторДокумента = ТекущийПользователь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСтоимостьРазмещения

&НаКлиенте
Процедура СтоимостьРазмещенияТарифныйПланПриИзменении(Элемент)

	ТекущаяСтрокаТЧ = Элементы.СтоимостьРазмещения.ТекущиеДанные;
	СтоимостьТарифногоПлана = ПолучитьЦенуРазмещенияВакансии(ТекущаяСтрокаТЧ.Сайт, ТекущаяСтрокаТЧ.ТарифныйПлан);
	Если СтоимостьТарифногоПлана = 0 Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Не установлена цена для тарифного плана  - " + ТекущаяСтрокаТЧ.ТарифныйПлан);
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрокаТЧ.Цена = СтоимостьТарифногоПлана;
	РассчитатьСуммуПоДокументу();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДополнительныеЗатраты

&НаКлиенте
Процедура ДополнительныеЗатратыКоличествоПриИзменении(Элемент)
	
	РассчитатьСуммуДополнительныхЗатрат();
	РассчитатьСуммуПоДокументу();
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЗатратыВидЗатратПриИзменении(Элемент)
	
	ТекущаяСтрокаТЧ = Элементы.ДополнительныеЗатраты.ТекущиеДанные;	
	СтоимостьУслуги = ПолучитьЦенуУслуги(ТекущаяСтрокаТЧ.Сайт, ТекущаяСтрокаТЧ.ВидЗатрат);
	
	Если СтоимостьУслуги = 0 Тогда
		ОбщегоНазначенияКлиент.ВывестиСообщение("Не установлена цена для вида затрат (услуги)  - " + ТекущаяСтрокаТЧ.ВидЗатрат);
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрокаТЧ.Цена = СтоимостьУслуги;
	
	РассчитатьСуммуДополнительныхЗатрат();
	РассчитатьСуммуПоДокументу();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьЦенуРазмещенияВакансии(Сайт, ТарифныйПлан)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("СайтВакансии", Сайт); 
	ПараметрыОтбора.Вставить("ТарифныйПлан", ТарифныйПлан);
	
	ЦенаВакансии = РегистрыСведений.ТарифыВакансий.ПолучитьПоследнее(, ПараметрыОтбора); 
	
	Возврат ЦенаВакансии.Цена; 	

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьЦенуУслуги(Сайт, Услуга)

	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("СайтВакансии", Сайт); 
	ПараметрыОтбора.Вставить("Услуга", Услуга);
	
	ЦенаУслуги = РегистрыСведений.ЦенаДополнительныхУслугВакансий.ПолучитьПоследнее(, ПараметрыОтбора); 
	
	Возврат ЦенаУслуги.Цена; 		

КонецФункции

&НаКлиенте
Процедура РассчитатьСуммуДополнительныхЗатрат()

	ТекущаяСтрокаТЧ = Элементы.ДополнительныеЗатраты.ТекущиеДанные;
	ТекущаяСтрокаТЧ.Сумма = ТекущаяСтрокаТЧ.Количество * ТекущаяСтрокаТЧ.Цена;	

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуПоДокументу()

	Объект.СуммаПоДокументу = Объект.СтоимостьРазмещения.Итог("Цена") + Объект.ДополнительныеЗатраты.Итог("Сумма");	

КонецПроцедуры

#КонецОбласти




