
Процедура ОбработкаПроведения(Отказ, Режим)

	ЗаписатьОценкиКандидатовВРегистр();
	ЗаписатьДвиженияКоличествоКандидатовПоЭтапам();
	ЗаписатьДвиженияСтатусыКандидатов();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПроведениеСобеседования") Тогда
		Вакансия = ДанныеЗаполнения.Вакансия;
		Кандидат = ДанныеЗаполнения.Кандидат;
		Основание = ДанныеЗаполнения.Ссылка;
		ИзменитьСтатусКандидата(ДанныеЗаполнения.Кандидат, Перечисления.ЭтапыПодбора.Тестирование);
	КонецЕсли;

КонецПроцедуры

Процедура ИзменитьСтатусКандидата(Кандидат, Статус)

	КандидатОбъект = Кандидат.ПолучитьОбъект();
	КандидатОбъект.ЭтапПодбора = Статус;
	КандидатОбъект.Записать();

КонецПроцедуры

Процедура ЗаписатьОценкиКандидатовВРегистр()

	// регистр накопления ОценкиКандидатов 
	Движения.ОценкиКандидатов.Записывать = Истина;
	Движение = Движения.ОценкиКандидатов.Добавить();
	Движение.Период = Дата;
	Движение.Вакансия = Вакансия;
	Движение.Кандидат = Кандидат;
	Движение.Анкета = АнкетаВопросов;
	Движение.Оценка = ОбщаяОценка;	

КонецПроцедуры

Процедура ЗаписатьДвиженияКоличествоКандидатовПоЭтапам()

	// регистр накопления КоличествоКандидатовПоЭтапам Приход
	Движения.КоличествоКандидатовПоЭтапам.Записывать = Истина;
	Движение = Движения.КоличествоКандидатовПоЭтапам.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Вакансия = Вакансия;
	Движение.Кандидат = Кандидат;
	Движение.Этап = Перечисления.ЭтапыПодбора.Тестирование;
	Если Статус = Перечисления.СтатусыТестирования.ВПроцессе Тогда
		Движение.ЭтапПройден = Ложь;
	ИначеЕсли Статус = Перечисления.СтатусыТестирования.Завершилось Тогда
		Движение.ЭтапПройден = Истина;
	КонецЕсли;
	Движение.ДатаИзмененияЭтапа = Дата;
	Движение.Количество = 1;

	// регистр накопления КоличествоКандидатовПоЭтапам Расход
	Движения.КоличествоКандидатовПоЭтапам.Записывать = Истина;
	Движение = Движения.КоличествоКандидатовПоЭтапам.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата;
	Движение.Вакансия = Вакансия;
	Движение.Кандидат = Кандидат;
	Движение.Этап = Перечисления.ЭтапыПодбора.СобеседованиеСИнициатором;
	Движение.ЭтапПройден = Истина;
	Движение.ДатаИзмененияЭтапа = Дата;
	Движение.Количество = 1;	

КонецПроцедуры

Процедура ЗаписатьДвиженияСтатусыКандидатов()

	// регистр сведений СтатусыКандидатов
	Движения.СтатусыКандидатов.Записывать = Истина;
	Движение = Движения.СтатусыКандидатов.Добавить();
	Движение.Период = Дата;
	Движение.Вакансия = Вакансия;
	Движение.Кандидат = Кандидат;
	Если Статус = Перечисления.СтатусыТестирования.ВПроцессе Тогда
		Движение.ЭтапПройден = Ложь;
	ИначеЕсли Статус = Перечисления.СтатусыТестирования.Завершилось Тогда
		Движение.ЭтапПройден = Истина;
	КонецЕсли;
	Движение.Статус = Перечисления.ЭтапыПодбора.Тестирование;

КонецПроцедуры

