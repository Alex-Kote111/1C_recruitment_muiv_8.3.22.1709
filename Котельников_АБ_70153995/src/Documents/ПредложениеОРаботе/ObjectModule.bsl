
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Вакансия") Тогда
		ГрафикРаботы = ДанныеЗаполнения.ГрафикРаботы;
		ДоВычетаНалога = ДанныеЗаполнения.ДоВычетаНалога;
		Должность = ДанныеЗаполнения.Должность;
		ЗарплатаДо = ДанныеЗаполнения.ОплатаТрудаДо;
		ЗарплатаОт = ДанныеЗаполнения.ОплатаТрудаОт;
		Подразделение = ДанныеЗаполнения.Подразделение;
		Вакансия = ДанныеЗаполнения.Ссылка;
		ТипЗанятости = ДанныеЗаполнения.ТипЗанятости;
		ФорматРаботы = ДанныеЗаполнения.ФорматРаботы;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	ЗаписатьДвиженияКоличествоКандидатовПоЭтапам();
	ЗаписатьДвиженияСтатусыКандидатов();

КонецПроцедуры

Процедура ЗаписатьДвиженияКоличествоКандидатовПоЭтапам()

	// регистр накопления КоличествоКандидатовПоЭтапам Приход
	Движения.КоличествоКандидатовПоЭтапам.Записывать = Истина;
	Движение = Движения.КоличествоКандидатовПоЭтапам.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Вакансия = Вакансия;
	Движение.Кандидат = Кандидат;
	
	Если Статус = Перечисления.СтатусыПредложенияОРаботе.СделаноПредложениеОРаботе Тогда
		Движение.Этап = Перечисления.ЭтапыПодбора.ПредложениеОРаботе;
		Движение.ЭтапПройден = Ложь;
	ИначеЕсли Статус = Перечисления.СтатусыПредложенияОРаботе.КандидатПринялПредложение Тогда
		Движение.Этап = Перечисления.ЭтапыПодбора.ПринялПредложениеОРаботе;
		Движение.ЭтапПройден = Истина;
	ИначеЕсли Статус = Перечисления.СтатусыПредложенияОРаботе.КандидатОтказался Тогда 
		Движение.Этап = Перечисления.ЭтапыПодбора.КандидатОтказалсяОтПредложения;
		Движение.ЭтапПройден = Ложь;
	КонецЕсли;
	Движение.ДатаИзмененияЭтапа = Дата;
	Движение.Количество = 1;

	// регистр накопления КоличествоКандидатовПоЭтапам Расход
	Движения.КоличествоКандидатовПоЭтапам.Записывать = Истина;
	Движение = Движения.КоличествоКандидатовПоЭтапам.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата;
	Движение.Вакансия = Вакансия;
	Движение.Кандидат = Кандидат;
	Движение.Этап = Перечисления.ЭтапыПодбора.Тестирование;
	Движение.ЭтапПройден = Истина;
	Движение.ДатаИзмененияЭтапа = Дата;
	Движение.Количество = 1;	

КонецПроцедуры

Процедура ЗаписатьДвиженияСтатусыКандидатов()

	// регистр сведений СтатусыКандидатов
	Движения.СтатусыКандидатов.Записывать = Истина;
	Движение = Движения.СтатусыКандидатов.Добавить();
	Движение.Период = Дата;
	Движение.Вакансия = Вакансия;
	Движение.Кандидат = Кандидат;
	Если Статус = Перечисления.СтатусыПредложенияОРаботе.СделаноПредложениеОРаботе Тогда
		Движение.Статус = Перечисления.ЭтапыПодбора.ПредложениеОРаботе;
		Движение.ЭтапПройден = Ложь;
	ИначеЕсли Статус = Перечисления.СтатусыПредложенияОРаботе.КандидатПринялПредложение Тогда
		Движение.Статус = Перечисления.ЭтапыПодбора.ПринялПредложениеОРаботе;
		Движение.ЭтапПройден = Истина;
	ИначеЕсли Статус = Перечисления.СтатусыПредложенияОРаботе.КандидатОтказался Тогда 
		Движение.Статус = Перечисления.ЭтапыПодбора.КандидатОтказалсяОтПредложения;
		Движение.ЭтапПройден = Ложь;
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Статус = Перечисления.СтатусыПредложенияОРаботе.КандидатПринялПредложение И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		УзелЗУП = ПланыОбмена.ПодборПерсоналаЗарплатаИУправлениеПерсоналом.НайтиПоКоду("ЗУП");
		ПланыОбмена.ЗарегистрироватьИзменения(УзелЗУП, Кандидат);
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда	
		УзелЗУП = ПланыОбмена.ПодборПерсоналаЗарплатаИУправлениеПерсоналом.НайтиПоКоду("ЗУП");
		ПланыОбмена.УдалитьРегистрациюИзменений(УзелЗУП, Кандидат);
	КонецЕсли;
	
КонецПроцедуры



